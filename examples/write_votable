#!/usr/bin/perl

use v5.24;

use warnings;

# write_votable - Sample program illustrating the use of the VOTable
# modules to build and print a VOTABLE document.

# External modules
use Astro::VO::VOTable;

# FIELD element names, and cell data.
my @field_names = qw(ID RA DEC);
my @rows = (
 	     ['A', '9.9', '8.8'],
 	     ['B', '7.7', '6.6'],
 	     ['C', '5.5', '4.4']
 	     );

# Create the VOTABLE document.
my $doc = Astro::VO::VOTable::Document->new or die;

# Get the VOTABLE element.
my $votable = $doc->get_VOTABLE or die;

# Create the RESOURCE element and add it to the VOTABLE.
my $resource = Astro::VO::VOTable::RESOURCE->new or die;
$votable->set_RESOURCE($resource);

# Create the DESCRIPTION element and its contents, and add it to the
# RESOURCE.
my $description = Astro::VO::VOTable::DESCRIPTION->new or die;
$description->set('This is another set of sample data in VOTABLE format.');

# Create the TABLE element and add it to the RESOURCE.
my $table = Astro::VO::VOTable::TABLE->new or die;
$resource->set_TABLE($table);

# Create and add the FIELD elements to the TABLE.
for my $name ( @field_names ) {
    my $field = Astro::VO::VOTable::FIELD->new or die;
    $field->set_name($name);
    $table->append_FIELD($field);
}

# Create and append the DATA element.
my $data = Astro::VO::VOTable::DATA->new or die;
$table->set_DATA($data);

# Create and append the TABLEDATA element.
my $tabledata = Astro::VO::VOTable::TABLEDATA->new or die;
$data->set_TABLEDATA($tabledata);

# Create and append each TR element, and each TD element.
for my $row (@rows) {
    my $tr = Astro::VO::VOTable::TR->new or die;
    for my $data ( @$row ) {
 	my $td = Astro::VO::VOTable::TD->new or die;
 	$td->set($data);
 	$tr->append_TD($td);
    }
    $tabledata->append_TR($tr);
}

# Print the finished document.
print $doc->toString;

# Print it nicer!
print "\n";
print $doc->toString(1);

# Print it with way too much whitespace!
print "\n";
print $doc->toString(2);
